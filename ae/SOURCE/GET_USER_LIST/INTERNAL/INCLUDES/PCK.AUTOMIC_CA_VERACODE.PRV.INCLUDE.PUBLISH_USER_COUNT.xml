<?xml version="1.0" encoding="ISO-8859-15" standalone="no"?><uc-export clientvers="11.0.0">
<JOBI name="PCK.AUTOMIC_CA_VERACODE.PRV.INCLUDE.PUBLISH_USER_COUNT">
<HEADER state="1">
<Title>Publish xml and user count</Title>
<ArchiveKey1/>
<ArchiveKey2/>
<OH_SubType/>
</HEADER>
<SCRIPT mode="1" state="1">
<MSCRI><![CDATA[:SET &STATUS# = GET_UC_OBJECT_STATUS(,,"RETCODE")
:SET &UC4RB_VER_RESPONSE# = ""
:SET &UC4RB_VER_USER_COUNT# = ""

:IF &STATUS# EQ 0
:            SET &NL# = UC_CRLF()
:            SET &HND# = PREP_PROCESS_FILE(&AGENT#,&UC4RB_TEMP_FILE_PATH#,,,,'UC_LOGIN=&LOGIN#')
:            PROCESS &HND#
:                 SET &LINE#=GET_PROCESS_LINE(&HND#)
:                 SET &UC4RB_VER_RESPONSE# = STR_CAT(&UC4RB_VER_RESPONSE#, &LINE#)
:                 SET &UC4RB_VER_RESPONSE# = STR_CAT(&UC4RB_VER_RESPONSE#, &NL#)
:            ENDPROCESS
:            CLOSE_PROCESS &HND#

!            Reading the xml
:            SET &XMLDOCU# = XML_OPEN(STRING,&UC4RB_VER_RESPONSE#)
!                 A bad XML string either leads to run time error, or returns a return code which can be read with sys_last_err_nr().
:                 SET &ERROR# = sys_last_err_nr()
:                 IF &ERROR# > 0
:                     SET &ERROR_DETAILS# = sys_last_err_ins()
:                     PRINT "ERROR: &ERROR_DETAILS#"
:                     MODIFY_STATE STATUS_TEXT="Error occured!!"
:                     MODIFY_STATE RETCODE=50
:                 ENDIF

!                 Read user list from the response
:                 SET &FIRST_CHILD# = XML_GET_FIRST_CHILD(&XMLDOCU#)
:                 SET &SIBLING# = XML_GET_NEXTSIBLING(&FIRST_CHILD#)
:                 SET &NODE_NAME# = XML_GET_NODE_NAME(&SIBLING#)
:                 IF &NODE_NAME# = "users"
:                       SET &UC4RB_VER_USER_LIST# = XML_GET_ATTRIBUTE(&SIBLING#, "@usernames")
:                 ELSE
:                       PRINT "ERROR: Unable to retrieve user list."
:                       MODIFY_STATE STATUS_TEXT="Unable to retrieve user list."
:                       MODIFY_STATE RETCODE=50
:                 ENDIF
:            XML_CLOSE

:            SET &UC4RB_VER_USER_COUNT# = 0
:            IF &UC4RB_VER_USER_LIST# <> ""
:                  SET &POS#=STR_FIND(&UC4RB_VER_USER_LIST#,",")
:                  WHILE &POS# NE 0
:                        SET &UC4RB_VER_USER_COUNT# = &UC4RB_VER_USER_COUNT# + 1
:                        SET &STAR_POINT# = &POS# + 1
:                        SET &POS#=STR_FIND(&UC4RB_VER_USER_LIST#,",",&STAR_POINT#)
:                  ENDWHILE

:                  IF &UC4RB_VER_USER_COUNT# <> 0
:                        SET &UC4RB_VER_USER_COUNT# = &UC4RB_VER_USER_COUNT# + 1
:                  ELSE
:                        SET &UC4RB_VER_USER_COUNT# = 1
:                  ENDIF
:            ELSE
:                  PRINT "No users found!!!!"
:            ENDIF

!            deleting temporary file
:            SET &UC4RB_DELETE_FILE#=&UC4RB_TEMP_FILE_PATH#
:            INC PCK.AUTOMIC_CA_VERACODE.PUB.INCLUDE.DELETE_FILE
:ENDIF

:PUBLISH &UC4RB_VER_RESPONSE#,,"WORKFLOW"
:PUBLISH &UC4RB_VER_USER_COUNT#,,"WORKFLOW"]]></MSCRI>
</SCRIPT>
<DOCU_Docu state="1" type="text">
<DOC/>
</DOCU_Docu>
</JOBI>
</uc-export>