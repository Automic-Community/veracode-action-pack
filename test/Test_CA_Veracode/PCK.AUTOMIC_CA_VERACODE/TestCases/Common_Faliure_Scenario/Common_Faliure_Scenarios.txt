*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        common_failure_scenarios
Test Template     Common Failure Scenarios
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    APIID             APIKEY        VeracodeCLILocation         ProxyHost        ProxyPort        ProxyUser        ProxyPassword        ApplicationID        Modules      ScanOptions    SandboxID
Assertion with Invalid API ID
                      [Tags]    main_scenarios
                      NOK       Invalid           ${API_KEY}    ${Veracode_CLI_Location}    ${Proxy_Host}    ${Proxy_Port}    ${Proxy_User}    ${Proxy_Password}    ${Application_ID}    ${Module}    ${EMPTY}       ${Sandbox_ID}

Assertion with invalid API Key
                      NOK       ${API_ID}         Invalid       ${Veracode_CLI_Location}    ${Proxy_Host}    ${Proxy_Port}    ${Proxy_User}    ${Proxy_Password}    ${Application_ID}    ${Module}    ${EMPTY}       ${Sandbox_ID}

Assertion with invalid Veracode CLI Location
                      NOK       ${API_ID}         ${API_KEY}    Invalid                     ${Proxy_Host}    ${Proxy_Port}    ${Proxy_User}    ${Proxy_Password}    ${Application_ID}    ${Module}    ${EMPTY}       ${Sandbox_ID}

Assertion with invalid Proxy Host
                      NOK       ${API_ID}         ${API_KEY}    ${Veracode_CLI_Location}    Invalid          ${Proxy_Port}    ${Proxy_User}    ${Proxy_Password}    ${Application_ID}    ${Module}    ${EMPTY}       ${Sandbox_ID}

Assertion with invalid Proxy Port
                      NOK       ${API_ID}         ${API_KEY}    ${Veracode_CLI_Location}    ${Proxy_Host}    Invalid          ${Proxy_User}    ${Proxy_Password}    ${Application_ID}    ${Module}    ${EMPTY}       ${Sandbox_ID}

Assert Rest API ID as mandatory field
                      NOK       ${EMPTY}          ${API_KEY}    ${Veracode_CLI_Location}    ${Proxy_Host}    ${Proxy_Port}    ${Proxy_User}    ${Proxy_Password}    ${Application_ID}    ${Module}    ${EMPTY}       ${Sandbox_ID}

Assert API Key as mandatory field
                      NOK       ${API_ID}         ${EMPTY}      ${Veracode_CLI_Location}    ${Proxy_Host}    ${Proxy_Port}    ${Proxy_User}    ${Proxy_Password}    ${Application_ID}    ${Module}    ${EMPTY}       ${Sandbox_ID}

Assert Veracode CLI Location as mandatory field
                      NOK       ${API_ID}         ${API_KEY}    ${EMPTY}                    ${Proxy_Host}    ${Proxy_Port}    ${Proxy_User}    ${Proxy_Password}    ${Application_ID}    ${Module}    ${EMPTY}       ${Sandbox_ID}

*** Keywords ***
Startup
    Log    *******Start Test "COMMON FAILURE SCENARIOS"*******
    Log    *******Connect to AE*******
    Connect AE

Common Failure Scenarios
    [Arguments]    ${OK_NOK}    ${APIID}    ${APIKEY}    ${VeracodeCLILocation}    ${ProxyHost}    ${ProxyPort}
    ...    ${ProxyUser}    ${ProxyPassword}    ${ApplicationID}    ${Modules}    ${ScanOptions}    ${SandboxID}
    [Documentation]    This test executes the common failure scenarios for action pack with Begin Scan action
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK, NOK.
    ...    -${APIID}: This field specifies API ID for authentication.
    ...    -${APIKEY}: This field specifies API key for authentication.
    ...    -${VeracodeCLILocation}:This field specifies the path for Veracode Java CLI.
    ...    -${ProxyHost}: This field specifies proxy host.
    ...    -${ProxyPort}: This field specifies proxy port.
    ...    -${ProxyUser}: This field specifies proxy username.
    ...    -${ProxyPassword}: This field specifies password to proxy username.
    ...    -${ApplicationID}: This field specifies application ID which will be scanned.
    ...    -${Modules}: This field specifies top-level module IDs separated by comma.
    ...    -${ScanOptions}: This field specifies option to scan modules.
    ...    -${SandboxID}: This field specifies the ID of the sandbox if you are performing a sandbox scan.
    Generate String
    Init Action    ${_BEGIN_SCAN_ACTION}
    Connect to Veracode
    Run Keyword If    '${APIID}' == 'Invalid'    Action Set    &UC4RB_VER_API_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VER_API_ID#    ${APIID}
    Run Keyword If    '${APIKEY}' == 'Invalid'    Action Set    &UC4RB_VER_API_KEY#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VER_API_KEY#    ${APIKEY}
    Run Keyword If    '${VeracodeCLILocation}' == 'Invalid'    Action Set    &UC4RB_VER_CLI_PATH#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VER_CLI_PATH#    ${VeracodeCLILocation}
    Run Keyword If    '${ProxyHost}' == 'Invalid'    Action Set    &UC4RB_VER_PROXY_HOST#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VER_PROXY_HOST#    ${ProxyHost}
    Run Keyword If    '${ProxyPort}' == 'Invalid'    Action Set    &UC4RB_VER_PROXY_PORT#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VER_PROXY_PORT#    ${ProxyPort}
    Action Set    &UC4RB_VER_PROXY_USER#    ${ProxyUser}
    Action Set    &UC4RB_VER_PROXY_PASS#    ${ProxyPassword}
    Action Set    &UC4RB_VER_APP_ID#    ${ApplicationID}
    Action Set    &UC4RB_VER_MODULES#    ${Modules}
    Action Set    &UC4RB_VER_SCAN_OPTIONS#    ${ScanOptions}
    Action Set    &UC4RB_VER_SANDBOX#    ${SandboxID}
    Action Execute
    Assert Failure

Teardown
    Log    *******End Test "COMMON FAILURE SCENARIOS"*******
