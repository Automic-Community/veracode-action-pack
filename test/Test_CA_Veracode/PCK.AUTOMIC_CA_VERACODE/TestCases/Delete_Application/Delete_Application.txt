*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        delete_application
Test Template     Delete Application Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ApplicationID
Delete Application with valid Application ID
                      [Tags]    main_scenarios
                      OK        ${Application_ID}

Delete Application while providing non-existing Application ID
                      NOK       Invalid

Assert Application ID as Mandatory Field
                      NOK       ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "DELETE APPLICATION"*******
    Log    *******Connect to AE*******
    Connect AE

Delete Application Template
    [Arguments]    ${OK_NOK}    ${ApplicationID}
    [Documentation]    This test starts Delete Application.
    ...    -Precondition: To execute valid test, Valid Connection Details should be known to user.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${ApplicationID}: This field specifies ID of the application you want to delete.
    Generate String
    Init Action    ${_DELETE_APPLICATION_ACTION}
    Connect to Veracode
    Connect to Proxy
    Run Keyword If    '${ApplicationID}' == 'Invalid'    Action Set    &UC4RB_VER_APP_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VER_APP_ID#    ${ApplicationID}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_VER_RESPONSE#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_VER_RESPONSE#

Teardown
    Log    *******End Test "DELETE APPLICATION"*******
