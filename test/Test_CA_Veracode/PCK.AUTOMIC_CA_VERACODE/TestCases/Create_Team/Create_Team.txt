*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        create_team
Test Template     Create Team Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    TeamName          Members
Create Team and also linking multiple members to it
                      [Tags]    main_scenarios
                      OK        ${Team_Name}      ${Team_Members}

Create Team and ands linking one member to it
                      OK        ${Team_Name_1}    ${Member}

Create Team while asserting members as optional field
                      OK        ${Team_Name_2}    ${EMPTY}

Create Team with already existing Team Name
                      NOK       ${Team_Name_2}    ${EMPTY}

Create Team while providing non-existing members
                      NOK       ${Team_Name_3}    Invalid

Create Team while providing multiple members and on of them is non-existing
                      NOK       ${Team_Name_3}    ${Team_Members},InvalidMember

Assert Team Name as Mandatory Field
                      NOK       ${EMPTY}          ${Team_Members}

*** Keywords ***
Startup
    Log    *******Start Test "CREATE TEAM"*******
    Log    *******Connect to AE*******
    Connect AE

Create Team Template
    [Arguments]    ${OK_NOK}    ${TeamName}    ${Members}
    [Documentation]    This test starts Create Team.
    ...    -Precondition: To execute valid test, Valid Connection Details should be known to user.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${TeamName}: This field specifies name of the team you are creating
    ...    -${Members}: This field specifies usernames of members as separated by comma.
    Generate String
    Init Action    ${_CREATE_TEAM_ACTION}
    Connect to Veracode
    Connect to Proxy
    Run Keyword If    '${TeamName}' == 'Invalid'    Action Set    &UC4RB_VER_TEAM#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VER_TEAM#    ${TeamName}
    Run Keyword If    '${Members}' == 'Invalid'    Action Set    &UC4RB_VER_MEMBER#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VER_MEMBER#    ${Members}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Count Value Should Be    &UC4RB_VER_TEAM_ID#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_VER_TEAM_ID#
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Count Value Should Be    &UC4RB_VER_RESPONSE#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_VER_RESPONSE#

Teardown
    Log    *******End Test "CREATE TEAM"*******
