*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        common_failure_scenarios
Test Template     Common Failure Scenarios
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    APIID             APIKEY        VeracodeCLILocation         TeamName        Members
Assertion with Invalid Rest API ID
                      [Tags]    main_scenarios
                      NOK       Invalid           ${API_KEY}    ${Veracode_CLI_Location}    ${Team_Name}    ${Team_Members}

Assertion with invalid API Key
                      NOK       ${API_ID}         Invalid       ${Veracode_CLI_Location}    ${Team_Name}    ${Team_Members}

Assertion with invalid Veracode CLI Location
                      NOK       ${API_ID}         ${API_KEY}    Invalid                     ${Team_Name}    ${Team_Members}

Assert Rest API ID as mandatory field
                      NOK       ${EMPTY}          ${API_KEY}    ${Veracode_CLI_Location}    ${Team_Name}    ${Team_Members}

Assert API Key as mandatory field
                      NOK       ${API_ID}         ${EMPTY}      ${Veracode_CLI_Location}    ${Team_Name}    ${Team_Members}

Assert Veracode CLI Location as mandatory field
                      NOK       ${API_ID}         ${API_KEY}    ${EMPTY}                    ${Team_Name}    ${Team_Members}

*** Keywords ***
Startup
    Log    *******Start Test "COMMON FAILURE SCENARIOS"*******
    Log    *******Connect to AE*******
    Connect AE

Common Failure Scenarios
    [Arguments]    ${OK_NOK}    ${APIID}    ${APIKEY}    ${VeracodeCLILocation}    ${TeamName}    ${Members}
    [Documentation]    This test executes the common failure scenarios for action pack with List Subsystems action
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK, NOK.
    ...    -${APIID}: This field specifies API ID for authentication.
    ...    -${APIKEY}: This field specifies API key for authentication.
    ...    -${VeracodeCLILocation}:This field specifies the path for Veracode Java CLI.
    ...    -${TeamName}: This field specifies name of the team you are creating. It is case sensitive.
    ...    -${Members}: This field specifies usernames of members as separated by comma.
    Generate String
    Init Action    ${_CREATE_TEAM_ACTION}
    Run Keyword If    '${APIID}' == 'Invalid'    Action Set    &UC4RB_VER_API_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VER_API_ID#    ${APIID    }
    Run Keyword If    '${APIKEY}' == 'Invalid'    Action Set    &UC4RB_VER_API_KEY#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VER_API_KEY#    ${APIKEY}
    Run Keyword If    '${VeracodeCLILocation}' == 'Invalid'    Action Set    &UC4RB_VER_CLI_PATH#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VER_CLI_PATH#    ${VeracodeCLILocation}
    Action Set    &UC4RB_VER_TEAM#    ${TeamName}
    Action Set    &UC4RB_VER_MEMBER#    ${Members}
    Connect to Proxy
    Action Execute
    Assert Failure

Teardown
    Log    *******End Test "COMMON FAILURE SCENARIOS"*******
