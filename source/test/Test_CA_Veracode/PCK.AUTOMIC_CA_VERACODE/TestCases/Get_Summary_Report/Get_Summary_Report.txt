*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        get_summary_report
Test Template     Get Summary Report Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    BuildID           Format    FileName
Get Summary Report with valid inputs
                      [Tags]    main_scenarios
                      OK        ${Build_ID}       PDF       ${File_Name}

Get Summary Report with valid inputs and not giving output directory
                      OK        ${Build_ID}       XML       ${EMPTY}

Get Summary Report with valid inputs and overwriting Output file
                      OK        ${Build_ID}       PDF       ${File_Name}

Get Summary Report with valid inputs and format is CSV
                      OK        ${Build_ID}       CSV       ${EMPTY}

Get Summary Report with Build ID as Invalid
                      NOK       Invalid           CSV       ${File_Name}

Get Summary Report with File Name as Invalid
                      NOK       ${Build_ID}       XML       wrong\\path

Assert Build ID as Mandatory Field
                      NOK       ${EMPTY}          PDF       ${File_Name}

*** Keywords ***
Startup
    Log    *******Start Test "GET SUMMARY REPORT"*******
    Log    *******Connect to AE*******
    Connect AE

Get Summary Report Template
    [Arguments]    ${OK_NOK}    ${BuildID}    ${Format}    ${FileName}
    [Documentation]    This test starts Get Build Info.
    ...    -Precondition: To execute valid test, Valid Connection Details should be known to user.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${BuildID}: This field specifies Build ID of the scan. If empty system gets recent static scan results.
    ...    -${Format}: This field specifies in which format do you want to get summary report.
    ...    -${FileName}:This field specifies file name including full path where report will be saved.
    Generate String
    Init Action    ${_GET_SUMMARY_REPORT_ACTION}
    Connect to Proxy
    Connect to Veracode
    Run Keyword If    '${BuildID}' == 'Invalid'    Action Set    &UC4RB_VER_BUILD_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VER_BUILD_ID#    ${BuildID}
    Action Set    &UC4RB_VER_FORMAT#    ${Format}
    Run Keyword If    '${FileName}' == 'Invalid'    Action Set    &UC4RB_VER_FILE_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VER_FILE_NAME#    ${FileName}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_VER_REPORT_PATH#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_VER_REPORT_PATH#

Teardown
    Log    *******End Test "GET SUMMARY REPORT"*******
